apply plugin: 'java'
apply plugin: 'maven'
description = "Mockito for Android"

Properties props = new Properties()
props.load(new FileInputStream(rootProject.file("version.properties")))

group = 'org.mockito'
archivesBaseName = 'mockito-android'
version = props['mockito.version']

jar {
    manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version

    from("${rootProject.projectDir}") {
        include "LICENSE"
        into "META-INF"
        expand(copyright: new Date().format("yyyy"), version: project.version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allJava.srcDirs
    include "**/*.java", "**/*.aj"
}

artifacts {
    archives sourcesJar
}

tasks.javadoc.enabled = true

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${rootProject.projectDir}/target/maven/repository")
        }
    }
}

dependencies {
    compile project.rootProject
    compile "net.bytebuddy:byte-buddy-android:1.5.5"
}
